buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
    }
}



apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

group = 'com.bertramlabs.plugins'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")



repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {
    implementation "org.apache.groovy:groovy-all:$groovyVersion"

    api project(':karman-core')
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'


    api 'org.apache.httpcomponents:httpcore:4.4.16'
    api 'org.apache.httpcomponents:httpclient:4.5.14'
    api 'commons-beanutils:commons-beanutils:1.9.4'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'karman-azure'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'karman-azure'
                    description 'Karman Storage Provider interface for Azure Cloud Storage api'
                    url 'https://github.com/bertramdev/karman-core'
                    scm {
                        url 'https://github.com/bertramdev/karman-core'
                        connection 'scm:https://bertramdev@github.com/bertramdev/karman-core.git'
                        developerConnection 'scm:git://github.com/bertramdev/karman-core.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'bwhiton'
                            name 'Bob Whiton'
                            email 'bwhiton@bcap.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}


java {
    withSourcesJar()
    withJavadocJar()
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    systemProperties System.properties
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
        events 'started', 'passed'
    }
}
