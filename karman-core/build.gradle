buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")



repositories {
    jcenter()
	mavenCentral()
}

configurations {
    provided
}
 

dependencies {
	provided 'org.codehaus.groovy:groovy-all:2.0.7'
	compile 'org.apache.ant:ant:1.7.0'
//	compile 'log4j:log4j:1.2.16'
    compile("commons-logging:commons-logging:1.1.1")
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'karman-core'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'asset-pipeline-core'
                    description 'Karman is a standardized Cloud File Management Interface for Groovy. It provides a unified means and simple DSL for accessing files on the cloud via extensible StorageProviders'
                    url 'https://github.com/bertramdev/karman-core'
                    scm {
                        url 'https://github.com/bertramdev/karman-core'
                        connection 'scm:https://bertramdev@github.com/bertramdev/karman-core.git'
                        developerConnection 'scm:git://github.com/bertramdev/karman-core.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

//Define bintrayUser and bintrayKey in ~/.gradle/gradle.properties
bintray {
    if(project.hasProperty('bintrayUser')) {
        user = bintrayUser
        key = bintrayKey
    }
    publications = ['maven']
    pkg {
        repo = 'karman'
        userOrg = 'bertramlabs'
        name = 'karman-core'
        licenses = ['Apache-2.0']
    }
}

groovydoc {
    source = 'src/main/groovy'
    footer = "Karman-Core is Licensed under the APACHE 2 License"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
